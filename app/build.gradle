apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion '24.0.3'

    defaultConfig {
        applicationId "com.udacity.gradle.builditbigger"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath true
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        free {
            applicationId 'com.udacity.gradle.builditbigger.free'
        }

        paid {
            applicationId 'com.udacity.gradle.builditbigger'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.google.code.findbugs'
    })
    testCompile 'junit:junit:4.12'

    compile 'com.android.support.test.espresso:espresso-idling-resource:2.2.2'
    compile 'com.android.support:appcompat-v7:25.0.0'
    freeCompile 'com.google.android.gms:play-services-ads:9.8.0'
    compile project(path: ':joker')
    compile project(path: ':backend', configuration: 'android-endpoints')
}


task testIntegration(dependsOn: [':backend:appengineRun', 'connectedAndroidTest']) {
    group 'verification'
    description 'Starts a local GAE dev server, runs android tests, and stops GAE server'
    finalizedBy ':backend:appengineStop'
}

afterEvaluate {
    connectedAndroidTest.mustRunAfter(':backend:appengineRun')
}
